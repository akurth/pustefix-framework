<?xml version="1.0" encoding="ISO-8859-1"?>

<!-- This is the master stylesheet. It is responsible for generating
     the final stylesheet that drives the page generation. It should
     be fairly general, i.e. the only modifications needed here
     besides bugfixes are the inclusion of other stylesheet to handle
     a specific "skinning" of elements. This is done via a
     customization xsl file that reads this file as xml and produces a
     specific version of the master.xsl (more below).
     
     Note that you can include additional stylesheets in two places
     here: in the "xsl:" section, where it applies to the generation
     of the final stylesheet, and in the "ixsl:" section, where it
     applies to the final html generation. It most often does NOT make
     sense to include more stylesheets in the final stylesheet, as all
     skinning and layout should happened in stages before. No skinning
     should be needed in the online version.

     NOTE: THIS IS NOT A WORKING MASTER STYLESHEET!  You need to
     process it with a customization stylesheet that handles the tags
     <cus:product/>, <cus:lang/>, <cus:navigation/> and <cus:custom_xsl/>.

     Let me repeat once again: NEVER EVER change anything here that
     doesn't apply to the most general case. If the need arises, do
     these changes by adding customizations to the custom xsl file
     mentioned above. Stay away from including skinning in the final
     generated stylesheet.
     
     The functionality contained in this file is for the most part the
     framehandling. Other than that it serves - as mentioned above -
     as the skeleton code for producing the final stylesheet.

     It includes include.xsl for the handling of the <pfx:include/> tag
     into both itself and the final stylesheet. This allows for servlets
     to use responses that are coded as <pfx:include/> tags.

     Besides that it includes forminput.xsl (Handling of html forms)
     and navigation.xsl (handling of automated navigations). Note:
     these are _not_ included into the final stylesheet.
     See these files for more info. -->

<xsl:stylesheet version="1.1"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:cus="http://www.schlund.de/pustefix/customize"
                xmlns:pfx="http://www.schlund.de/pustefix/core"
		xmlns:ixsl="http://www.w3.org/1999/XSL/TransformOutputAlias">

  <xsl:namespace-alias stylesheet-prefix="ixsl" result-prefix="xsl"/>
  <xsl:output method="xml" encoding="ISO-8859-1" indent="yes"/>

  <!-- the copy rule -->
  <xsl:include href="core/xsl/default_copy.xsl"/>
  
  <xsl:param name="outputmethod">html</xsl:param>
  <xsl:param name="outputencoding">iso-8859-1</xsl:param>
  <xsl:param name="outputdoctype-public"/>
  <xsl:param name="outputdoctype-system"/>

  <xsl:param name="stylesheets_to_include"/>
  
  <!-- Needed for includes to work. Remember to include this in the resulting stylesheet, too! -->
  <xsl:param name="lang"><cus:lang/></xsl:param>
  <xsl:param name="product"><cus:product/></xsl:param>

  <xsl:include href="core/xsl/include.xsl"/>
  
  <!-- Needed for navibuttons to work. Normally not needed in the resulting stylesheet -->
  <xsl:param name="navigation"><cus:navigation/></xsl:param>
  <xsl:param name="page"/>
  <xsl:param name="navitree" select="document($navigation)/make/navigation"/>

  <xsl:include href="core/xsl/navigation.xsl"/>

  <!-- general utility tags -->
  <xsl:include href="core/xsl/utils.xsl"/>

  <!-- Needed for input fields, radio/checkbuttons etc. -->
  <xsl:include href="core/xsl/forminput.xsl"/>

  <cus:custom_xsl/>

  <xsl:key name="frameset_key" match="pfx:frameset" use="'fset'"/>
  <xsl:key name="frame_key"    match="pfx:frame"    use="'frame'"/>

  <xsl:template match="/">
    <ixsl:stylesheet version="1.1" exclude-result-prefixes="pfx cus xsl">

      <ixsl:output indent="no" >
        <xsl:if test="not($outputmethod = '')">
          <xsl:attribute name="method"><xsl:value-of select="$outputmethod"/></xsl:attribute>
        </xsl:if>
        <xsl:if test="not($outputencoding = '')">
          <xsl:attribute name="encoding"><xsl:value-of select="$outputencoding"/></xsl:attribute>
        </xsl:if>
        <xsl:if test="not($outputdoctype-system = '')">
          <xsl:attribute name="doctype-system"><xsl:value-of select="$outputdoctype-system"/></xsl:attribute>
        </xsl:if>
        <xsl:if test="not($outputdoctype-public = '')">
          <xsl:attribute name="doctype-public"><xsl:value-of select="$outputdoctype-public"/></xsl:attribute>
        </xsl:if>
      </ixsl:output>
      
      <!-- these parameters will always be passed in by the servlet -->
      <!-- e.g. /xml/static/FOOBAR;jsessionid=1E668C65F42697962A31177EB5319D8B.foo -->
      <ixsl:param name="__uri"/>
      
      <!-- e.g. jsessionid=1E668C65F42697962A31177EB5319D8B.foo -->
      <!--<ixsl:param name="__sessid"/>--> <!-- defined in include.xsl.in -->

      <!-- e.g. 1E668C65F42697962A31177EB5319D8B.foo -->
      <!--
          This is used whenever a session id has to be passed to the
          outside for being able to jump back into the
          application. When not running under SSL, this is the usual
          session id.  Whenever running under it's the (now invalid)
          parent session id, that will still allow to jump back into
          the running SSL session if the client can be identified by a
          also supplied secure cookie. (See ServletManager for
          initimidating details)
      -->
      <ixsl:param name="__external_session_ref"/>
      
      <!-- e.g. /xml/static -->
      <ixsl:param name="__servletpath"/>

      <ixsl:param name="__remote_addr"/>
      <ixsl:param name="__server_name"/>

      <ixsl:param name="page"><xsl:value-of select="$page"/></ixsl:param>
      <ixsl:param name="pageflow"/>
      <!-- This will be routed through by the servlet
      (needed for frame handling to work) -->
      <ixsl:param name="__frame">_top</ixsl:param>

      <ixsl:param name="__reusestamp">-1</ixsl:param>
      
      <!-- Needed for includes to work -->
      <ixsl:param name="lang"><xsl:value-of select="$lang"/></ixsl:param>
      <ixsl:param name="product"><xsl:value-of select="$product"/></ixsl:param>
      <ixsl:include href="core/xsl/include.xsl"/>

      <!-- the copy rule -->
      <ixsl:include href="core/xsl/default_copy.xsl"/>

      <xsl:call-template name="gen_ixsl_include">
        <xsl:with-param name="ssheets"><xsl:value-of select="normalize-space($stylesheets_to_include)"/></xsl:with-param>
      </xsl:call-template>

      <ixsl:template match="/">
        <xsl:choose>
          <!-- <xsl:when test="//frameset"> -->
          <xsl:when test="key('frameset_key','fset')">
            <ixsl:choose>
              <ixsl:when test="$__frame = '_top'"> 
                <html>
                  <xsl:apply-templates select="/pfx:document/node()"/>
                </html>
              </ixsl:when>
              <xsl:for-each select="key('frame_key','frame')"> 
                <xsl:choose>
                  <xsl:when test="not(./pfx:frameset)">
                    <ixsl:when test="$__frame = '{./@name}'">
                      <xsl:apply-templates select="./node()"/>
                    </ixsl:when>
                  </xsl:when>
                  <xsl:otherwise>
                    <ixsl:when test="$__frame = '{./@name}'">
                      <html>
                        <head/>
			<xsl:apply-templates select="./pfx:frameset"/>
                      </html>
                    </ixsl:when>
                  </xsl:otherwise>
                </xsl:choose>
              </xsl:for-each>
            </ixsl:choose>
          </xsl:when>
          <xsl:otherwise> <!-- no frames defined! -->
            <xsl:apply-templates select="/pfx:document/node()"/>
          </xsl:otherwise>
        </xsl:choose>
      </ixsl:template>
    </ixsl:stylesheet>
  </xsl:template>

  <!-- this is needed... it allows to "comment out" blocks without loosing them during the trafo steps -->
  <xsl:template match="pfx:rem">
   <cus:documentation>
    <responsible>JTL</responsible>
    <description>
      Allows to comment out blocks without loosing them during the transformation steps
    </description>
  </cus:documentation>
  </xsl:template>

  <xsl:template match="pfx:head">
  <cus:documentation>
    <responsible>JTL</responsible>
      <description>creates the HTML head. The javascript library baselib.js will be included automatically</description>
      <pfx:head>
        <title>PFIXCORE Sample</title>
      </pfx:head>
      <example>
        <input>
          <xmlcode>
            <pfx:head>
              <title>PFIXCORE Sample</title>
            </pfx:head>
          </xmlcode>
        </input>
        <output>
          <xmlcode><head>
              <meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1"/>
              <script language="JavaScript" src="/core/script/baselib.js" type="text/javascript"></script>
              <title>PFIXCORE Sample</title></head>
          </xmlcode>
        </output>
      </example>
    </cus:documentation>  
    <head>
      <script language="JavaScript" src="/core/script/baselib.js" type="text/javascript"/>
      <xsl:apply-templates select="./node()"/>
    </head>
  </xsl:template>
  
  <xsl:template match="pfx:script">
  <cus:documentation>
    <responsible>JTL</responsible>
      <description>A script block will be inserted. The script type is JavaScript if no language attribute is given. When using
      complex JavaScript, you better put a  &lt;[CDATA[-Block around your Script, so that the
      XML-Document can not be destroyed.
      </description>

       <example>
        <input>
          <xmlcode>
            <pfx:script>
              function helloWorld() {
                 alert('Hello World');
              }
        
            </pfx:script>
          </xmlcode>
        </input>
        <output>
          <xmlcode>
            <script language="JavaScript">
              function helloWorld() {
                 alert('Hello World');
              }
            </script>
          </xmlcode>
        </output>
      </example>
      
   </cus:documentation>   
    <script>
      <xsl:attribute name="language">JavaScript</xsl:attribute>
      <xsl:copy-of select="@*"/>
      <ixsl:comment>
	<xsl:copy-of select="./node()"/>
	//</ixsl:comment>
    </script>
  </xsl:template>
  
  <xsl:template match="pfx:frameset">
  <cus:documentation>
    <responsible>JTL</responsible>
      <description>creates a frameset. Standard value for frameborder=0, framespacing=0, border=0. If you want to change
        the default settings just add the attributes, as all attributes will be copied to the resulting html framset tag.
      </description>

      <children>
        <name>pfx:frame</name>
        <description>See pfx:frame</description>        
      </children>

      <example>
        <input>
          <xmlcode>
            <pfx:frameset rows="50,*">
              <pfx:frame name="top_navi">
                <html>
                  <head>
                    <style type="text/css">
                      <pfx:include href="core/txt/styles.xml" part="standard"/>
                      ...
                    </style>
                  </head>
                  <body>
                    <pfx:include href="sample1/txt/navigation.xml" part="top"/>
                  </body>
                </html>
              </pfx:frame>
              <pfx:frame name="bottom">
                <html>
                  <head>
                    <style type="text/css">
                      <pfx:include href="core/txt/styles.xml" part="standard"/>
                    </style>
                  </head>
                  <body>
                    <pfx:maincontent part="content" path="sample1/txt/pages" prefix="main_"/>
                  </body>
                </html>
              </pfx:frame>
            </pfx:frameset>
          </xmlcode>
        </input>
        <output>
          <xmlcode>
            <frameset frameborder="0" framespacing="0" border="0" rows="50,*">
              <frame scrolling="auto" marginwidth="1" marginheight="1" name="top_navi" src="/xml/config;sessionid=12345.foo?__frame=top_navi&amp;__reuse=0123"/>
               <frame scrolling="auto" marginwidth="1" marginheight="1" name="bottom" src="/xml/config;jsessionid=12345F.foo?__frame=bottom&amp;__reuse=0123"/>
            </frameset>
          </xmlcode>
        </output>
      </example>
  </cus:documentation>

    <frameset frameborder="0" framespacing="0" border="0">
      <xsl:copy-of select="./@*"/>
      <xsl:apply-templates select="./pfx:frameset | ./pfx:frame"/>
    </frameset>
  </xsl:template>
  
  <xsl:template match="pfx:frame">
    <cus:documentation>
      <responsible>JTL</responsible>
      <description>Always a child of pfx:frameset.
        Inserts a html frame. Standard value for scrolling=auto,
        marginwidth=1, marginheight=1. If you want to change the default settings,
        add the attributes as all attributes will be copied to the resulting frame tag. 
        Note that you don't use a src attribute with pfx:frame! The frame content will be the child nodes of
        the pfx:frame tag.
      </description>

      <example>
        <input>
          <xmlcode>
            <pfx:frameset rows="50,*">
              <pfx:frame name="top_navi">
                <html>
                  <head>
                    <style type="text/css">
                      <pfx:include href="core/txt/styles.xml" part="standard"/>
                      ...
                    </style>
                  </head>
                  <body>
                    <pfx:include href="sample1/txt/navigation.xml" part="top"/>
                  </body>
                </html>
              </pfx:frame>
              <pfx:frame name="bottom">
                <html>
                  <head>
                    <style type="text/css">
                      <pfx:include href="core/txt/styles.xml" part="standard"/>
                    </style>
                  </head>
                  <body>
                    <pfx:maincontent part="content" path="sample1/txt/pages" prefix="main_"/>
                  </body>
                </html>
              </pfx:frame>
            </pfx:frameset>
          </xmlcode>
        </input>
        <output>
          <xmlcode>
            <frameset frameborder="0" framespacing="0" border="0" rows="50,*">
               <frame scrolling="auto" marginwidth="1" marginheight="1" name="top_navi" src="/xml/config;sessionid=12345.foo?__frame=top_navi&amp;__reuse=0123"/>
               <frame scrolling="auto" marginwidth="1" marginheight="1" name="bottom" src="/xml/config;jsessionid=12345F.foo?__frame=bottom&amp;__reuse=0123"/>
            </frameset>
          </xmlcode>
        </output>
      </example>

      
    </cus:documentation>
    
    <frame scrolling="auto" marginwidth="1" marginheight="1">
      <xsl:copy-of select="./@*[name()!='noresize']"/>
      <xsl:if test="@noresize!='false'">
	<xsl:attribute name="noresize">1</xsl:attribute>
      </xsl:if>
      <xsl:choose>
        <xsl:when test="not(./pfx:frameset) and .//pfx:external">
	  <xsl:choose>
	    <xsl:when test=".//pfx:external[position()=1]/@src">
	      <xsl:copy-of select=".//pfx:external[position()=1]/@src"/>
	    </xsl:when>
	    <xsl:otherwise>
	      <xsl:copy-of select=".//pfx:external[position()=1]/node()"/>
	    </xsl:otherwise>
	  </xsl:choose>
        </xsl:when>
        <xsl:otherwise>
          <ixsl:attribute name="src">
            <ixsl:value-of select="$__uri"/>?__frame=<xsl:value-of select="@name"/>&amp;__reuse=<ixsl:value-of select="$__reusestamp"/>
            <ixsl:if test="/formresult/frameanchor[@frame = '{@name}']">#<ixsl:value-of select="/formresult/frameanchor[@frame = '{@name}']/@anchor"/></ixsl:if>
          </ixsl:attribute>
        </xsl:otherwise>
      </xsl:choose>
    </frame>
  </xsl:template>


  <xsl:template name="gen_ixsl_include">
    <xsl:param name="ssheets"/>
    <xsl:variable name="first">
      <xsl:value-of select="normalize-space(substring-before(concat($ssheets, ' '), ' '))"/>
    </xsl:variable>
    <xsl:variable name="rest">
      <xsl:value-of select="normalize-space(substring-after($ssheets, ' '))"/>
    </xsl:variable>
    <xsl:if test="$first != ''">
      <ixsl:include>
        <xsl:attribute name="href"><xsl:value-of select="$first"/></xsl:attribute>
      </ixsl:include><xsl:text>
      </xsl:text>
    </xsl:if>
    <xsl:if test="$rest != ''">
      <xsl:call-template name="gen_ixsl_include">
        <xsl:with-param name="ssheets">
          <xsl:value-of select="$rest"/>
        </xsl:with-param>
      </xsl:call-template>
    </xsl:if>
  </xsl:template>


</xsl:stylesheet>

<!-- 
Local Variables: 
mode: xsl
End: 
--> 
